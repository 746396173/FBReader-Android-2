buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:1.3.0'
	}
}

apply plugin: 'com.android.application'

dependencies {
	compile project(':plugin/image-formats')
}

android {
	compileSdkVersion 23
	buildToolsVersion '23.0.1'

	lintOptions {
		abortOnError false
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	sourceSets {
		main.java.srcDirs = ['src/main/java']
		main.aidl.srcDirs = ['src/main/java']
		main.res.srcDirs = ['src/main/res']
		main.jniLibs.srcDirs = ['src/main/libs']
		main.jni.srcDirs = []
		main.manifest.srcFile 'src/main/AndroidManifest.xml'
	}

	signingConfigs {
		release {
			storeFile file(RELEASE_STORE_FILE)
			storePassword RELEASE_STORE_PASSWORD
			keyAlias RELEASE_KEY_ALIAS
			keyPassword RELEASE_KEY_PASSWORD
		}
	}

	defaultConfig {
		minSdkVersion 11
		targetSdkVersion 17
		versionCode 0
		versionName new File("$projectDir/../image-formats/VERSION").text.trim()
	}

	buildTypes {
		release {
			minifyEnabled false
			signingConfig signingConfigs.release
		}
	}

	task ndkBuild(type: Exec) {
		def ndkDir = plugins.getPlugin('com.android.application').sdkHandler.getNdkFolder()
		commandLine "$ndkDir/ndk-build", '-C', file('src/main/jni').absolutePath
	}

	tasks.withType(JavaCompile) {
		compileTask -> compileTask.dependsOn ndkBuild
	}

	clean {
		delete 'src/main/obj'
		delete 'src/main/libs'
	}
}

android.applicationVariants.all { variant ->
	def baseCode = 0
	def version = versionName
	if (version.matches('\\d*\\.\\d* beta \\d*')) {
		def parts = version.split('[ .]')
		baseCode = parts[0].toInteger() * 1000000 + parts[1].toInteger() * 10000 - 5000 + parts[3].toInteger() * 100
	} else if (version.matches('\\d*\\.\\d*\\.\\d*')) {
		def parts = version.split('\\.')
		baseCode = parts[0].toInteger() * 1000000 + parts[1].toInteger() * 10000 + parts[2].toInteger() * 100
	} else if (version.matches('\\d*\\.\\d*')) {
		def parts = version.split('\\.')
		baseCode = parts[0].toInteger() * 1000000 + parts[1].toInteger() * 10000
	} else {
		throw new IllegalArgumentException('Unsupported version format: ' + version)
	}

	variant.mergedFlavor.versionCode = baseCode
}
